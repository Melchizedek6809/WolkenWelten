(define cddr (λ (p) "Returns the cdr of the cdr of pair p"
	(cdr (cdr p))))

(define cadr (λ (p) "Returns the car of the cdr of pair p"
	(car (cdr p))))

(define caddr (λ (p) "Returns the car of the cdr of the cdr of pair p"
	(car (cdr (cdr p)))))

(define number? (λ (a) "Returns #t if a is a number"
	(or (int? a) (float? a) (vec? a))))

(define empty? (λ (a) "Returns #t if a is empty"
	(nil? a)))

(define last? (λ (a) "Returns #t if a is the last pair in a list"
	(nil? (cdr a))))

(define pos? (λ (a) "Returns #t if a is positive"
	(>= a 0)))

(define neg? (λ (a) "Returns #t if a is negative"
	(< a 0)))

(define there-exists? (λ (l pred) "Applies predicate to each element and return #t if it holds true for any element, otherwise #f"
	(cond ((nil? l) #f)
	      ((pred (car l)) #t)
	      (#t (there-exists? (cdr l) pred)))))

(define for-all? (λ (l pred) "Applies predicate to each element returns #t if it holds true for every element, otherwise #f"
	(cond ((nil? l) #t)
	      ((not (pred (car l))) #f)
	      (#t (for-all? (cdr l) pred)))))

(define last-pair (λ (l) "Returns the last pair of list l"
	(if (nil? (cdr l)) l (last-pair (cdr l)))))

(define except-last-pair (λ (l r) "Returns a copy of li without the last pair"
	(if (nil? (cdr l)) (reverse r) (except-last-pair (cdr l) (cons (car l) r)))))

(define make-list (λ (i e l) "Returns a list of i elements containing e in every car"
	(if (<= i 0) l (make-list (-- i) e (cons e l)))))

(define reduce (λ (o l s) "Combines all elements in l using operation o and starting value s"
	(if (nil? l) s (reduce o (cdr l) (o s (car l))))))

(define list-ref (λ (l i) "Returns the the element of list l at location i"
	(cond ((nil? l) #nil)
	      ((<= i 0) (car l))
              (#t (list-ref (cdr l) (-- i))))))

(define ++ (λ (i) "Returns its argument + 1"
	(+ i 1)))

(define -- (λ (i) "Returns its argument - 1"
	(- i 1)))

(define reverse (λ (l r) "Returns the list l in reverse order"
	(if (nil? l) r (reverse (cdr l) (cons (car l) r)))))

(define list-length (λ (a t) "Returns the length of list a"
	(if (nil? a) t (list-length (cdr a) (++ t)))))

(define length (λ (a) "Returns the length of a"
	(cond ((string? a) (str-len a))
              ((pair? a) (list-length a))
              (#t 0))))

(define filter (λ (p l) "Runs predicate p over every item in list l and returns a list consiting solely of items where p is true"
        (cond ((nil? l) #nil)
              ((p (car l)) (cons (car l) (filter p (cdr l))))
              (#t                       (filter p (cdr l))))))

(define map (λ (f l) "Runs f over every item in list l and returns the resulting list"
	(if (nil? l) l (cons (f (car l)) (map f (cdr l))))))

(define append-iter (λ (a b) "Iterator for append"
	(if (nil? a) b (append-iter (cdr a) (cons (car a) b)))))
(define append (λ (a b) "Appends to two lists a and b together"
	(append-iter (reverse a) b)))

(define sublist (λ (l start end ret) "Returns a new list containing all elements of l from start to end"
	(cond ((nil?    l) (reverse ret))
              ((neg?  end) (sublist      l      start  (+ (length l) end)))
              ((zero? end) (reverse ret))
              ((> start 0) (sublist (cdr l) (-- start) (-- end) #nil))
              ((> end   0) (sublist (cdr l) 0          (-- end) (cons (car l) ret))))))

(define list-head (λ (l k) "Returns the first k elemnts of list l"
	(sublist l 0 k)))

(define list-tail (λ (l k) "Returns the sublist of l obtained by omitting the first l elements"
	(sublist l k (length l))))

(define member (λ (m l) "Returns the first pair of list l whose car is equal to m"
	(cond ((nil? l) #f)
              ((eq? (car l) m) l)
              (#t (member m (cdr l))))))

(define delete (λ (e l) "Returns a filtered list l with all elements equal to e omitted"
	(filter (λ (a) (not (eq? a e))) l)))

(define describe (λ (a) "Returns the DocString to its argument, if available"
	(if (string? (car (cl-text a))) (car (cl-text a)) "No DocString found")))

(define symbol-table (λ (i r) "Returns all a list of all symbols defined accessible from the current closure"
	(if (nil? (cl i)) r (symbol-table (++ i) (map (λ (a) (car a)) (cl i))))))

(define display (λ (a) "Displays its argument"
	(print a) #nil))

(define newline (λ () "Prints a single line feed character"
	(display "\n")))

(define list (λ (...l) "Returns its arguments as a list"
	...l))

(define when (δ (c &...expr) "Evaluates l and returns the last result if c is #t"
	(cond (c (eval (cons 'begin &...expr))) (#t #f))))

(define yield-queue '())
(define yield (λ (pred expr) "Executes expr once pred is true, returns immediately otherwise"
	(cond ((pred) (expr) #t)
              (#t     (set! yield-queue (cons (cons 'yield (cons pred (cons expr #nil))) yield-queue))))))

(define yield-run-iter (λ (l) "Iter for yield-run"
	(cond ((nil? l) #t)
	      (#t (eval (car l)) (yield-run-iter (cdr l))))))
(define yield-run (λ () "Executes pending coroutines if their predicate now evaluates to #t"
	(cond ((nil? (car yield-queue)) #f)
	      (#t (let ((queue yield-queue)) (set! yield-queue '()) (yield-run-iter queue))))))

(define timeout (λ (millis) "Returns a function that evaluates to true once millis milliseconds have passed"
	(let ((goal (+ (msecs) millis))) (λ () (> (msecs) goal)))))

(define help (λ () "Returns a list describing every function accesible"
	(map (λ (a) (cat a "\n  " (describe a) "\n")) (symbol-table))))

(define arr-fill! (λ (a v i) "Fills array a with value v"
	(cond ((>= i (arr-length a)) a)
	      (#t (arr-set! a i v) (arr-fill! a v (++ i))))))

(define ansi-wrap (λ (c s) "Wraps string s in the ansi color c"
	(cat (ansi-fg c) s (ansi-fg 15))))

(define ansi-grey (λ (...s) "Wraps its arguments as a string in the a red ansi color code"
	(ansi-wrap 8 (apply cat ...s))))
(define ansi-red (λ (...s) "Wraps its arguments as a string in the a red ansi color code"
	(ansi-wrap 9 (apply cat ...s))))
(define ansi-green (λ (...s) "Wraps its arguments as a string in the a green ansi color code"
	(ansi-wrap 10 (apply cat ...s))))
(define ansi-yellow (λ (...s) "Wraps its arguments as a string in the a yellow ansi color code"
	(ansi-wrap 11 (apply cat ...s))))
(define ansi-blue (λ (...s) "Wraps its arguments as a string in the a blue ansi color code"
	(ansi-wrap 12 (apply cat ...s))))
(define ansi-pink (λ (...s) "Wraps its arguments as a string in the a pink ansi color code"
	(ansi-wrap 13 (apply cat ...s))))
(define ansi-cyan (λ (...s) "Wraps its arguments as a string in the a cyan ansi color code"
	(ansi-wrap 14 (apply cat ...s))))
(define ansi-white (λ (...s) "Wraps its arguments as a string in the a white ansi color code"
	(ansi-wrap 15 (apply cat ...s))))
