pkgname=mingw-w64-sdl2_mixer
pkgver=2.0.4
pkgrel=3
pkgdesc="A simple multi-channel audio mixer (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_mixer"
license=("zlib")
depends=(mingw-w64-crt mingw-w64-sdl2)
makedepends=(sed mingw-w64-gcc mingw-w64-configure)
options=(staticlibs strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-$pkgver.tar.gz")
md5sums=('a36e8410cac46b00a4d01752b32c3eb1')

_architectures="x86_64-w64-mingw32"

build() {
	cd "${srcdir}/SDL2_mixer-${pkgver}"

	# link ssp (needed because mingw env's -fstack-protection)
	export LDFLAGS="-lssp"

	sed -i 's/"modplug\.h"/"libmodplug\/modplug.h"/' music_modplug.h
	for _arch in ${_architectures}; do
		mkdir -p build-${_arch} && pushd build-${_arch}
		${_arch}-configure --enable-shared \
		--enable-static \
		--disable-music-ogg-shared \
		--disable-music-ogg \
		--disable-music-flac-shared \
		--disable-music-flac \
		--disable-music-midi-fluidsynth \
		--disable-music-midi-fluidsynth-shared \
		--disable-music-mod \
		--disable-music-mod-modplug \
		--disable-music-mp3 \
		--disable-music-mp3-mad-gpl \
		--disable-music-mp3-mpg123 \
		--disable-music-mp3-mpg123-shared \
		--disable-music-opus \
		--disable-music-opus-shared
		make
		popd
	done
}

package() {
	for _arch in ${_architectures}; do
		cd "${srcdir}/SDL2_mixer-${pkgver}/build-${_arch}"
		make DESTDIR="$pkgdir" install
		${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
		${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
	done
}
